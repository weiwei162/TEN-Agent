#
# Copyright Â© 2025 Agora
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0, with certain conditions.
# Refer to the "LICENSE" file in the root directory for more information.
#
import("//build/ten_runtime/feature/test.gni")
import("//build/ten_runtime/ten.gni")

ten_package_test_prepare_app("long_running_app") {
  src_app = "default_app_cpp"
  src_app_language = "cpp"
  generated_app_src_root_dir_name = "long_running_app"

  replace_paths_after_install_app = [
    "long_running_app/manifest.json",
    "long_running_app/property.json",

    "long_running_app/ten_packages/extension/extension_1/manifest.json",
    "long_running_app/ten_packages/extension/extension_1/property.json",
    "long_running_app/ten_packages/extension/extension_1/src/main.cc",
    "long_running_app/ten_packages/extension/extension_1/BUILD.gn",

    "long_running_app/ten_packages/extension/extension_2/manifest.json",
    "long_running_app/ten_packages/extension/extension_2/property.json",
    "long_running_app/ten_packages/extension/extension_2/src/main.cc",
    "long_running_app/ten_packages/extension/extension_2/BUILD.gn",
  ]

  replace_paths_after_install_all = []

  if (ten_enable_ten_manager) {
    deps = [
      "//core/src/ten_manager",
      "//core/src/ten_runtime:upload_ten_runtime_system_package_to_server",
      "//packages/core_apps/default_app_cpp:upload_default_app_cpp_to_server",
      "//packages/core_protocols/msgpack:upload_protocol_msgpack_to_server",
    ]
  }
}

ten_package_test_prepare_auxiliary_resources("long_running_app_test_files") {
  resources = [
    "__init__.py",
    "start.py",
  ]

  utils_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                            [
                              "--dir",
                              rebase_path("//tests/utils/**/*"),
                              "--dir-base",
                              rebase_path("//tests/utils"),
                              "--recursive",
                              "--only-output-file",
                            ],
                            "json")

  foreach(utils_file, utils_files) {
    utils_file_rel_path = utils_file.relative_path
    resources +=
        [ "//tests/utils/${utils_file_rel_path}=>utils/${utils_file_rel_path}" ]
  }
}

group("long_running") {
  deps = [
    ":long_running_app",
    ":long_running_app_test_files",
  ]
}
